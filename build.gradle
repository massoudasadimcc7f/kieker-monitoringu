subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	version = '1.2.0-SNAPSHOT'
	
	repositories {
		mavenCentral()
		jcenter()
		maven { url 'https://jitpack.io' }
	}

	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
	
	apply plugin: 'pmd'
	pmd { 
		ignoreFailures = true

		ruleSets = []
		ruleSetFiles = files('../kieker-trace-diagnosis-release-engineering/config/pmd/ruleset.xml')
		
		toolVersion = '5.3.3'
	}

	apply plugin: 'checkstyle'
	checkstyle {
		configFile = file('../kieker-trace-diagnosis-release-engineering/config/checkstyle/checkstyle.xml')
		
		ignoreFailures = true
		showViolations = false
	}
		
	apply plugin: 'findbugs'
	findbugs { 
		ignoreFailures = true 
		effort = "max"
		reportLevel = "low"
	}
}

def regexpReplaceInFile(File file, String searchExp, String replaceExp) {
	String contents = file.getText('UTF-8')
	contents = contents.replaceAll(searchExp, replaceExp)
	file.write(contents, 'UTF-8')
}

task updateLicenseHeaderYear() << {
	FileTree tree = fileTree(dir: 'src', include: '**/*.java')
	tree.each { File file ->
		regexpReplaceInFile(file, 'Copyright 20\\d\\d Kieker Project', 'Copyright 2015-2016 Kieker Project')
	}
}
